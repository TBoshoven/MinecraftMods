/*
Build script for all mods.
In principle, building a mod is as simple as adding an empty build.gradle to the mod's directory and updating
settings.gradle.
*/

// For those who want the bleeding edge
buildscript {
    repositories {
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

subprojects {
    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'idea'

    version = "1.12.2-DEV.0"
    group = "com.tomboshoven.minecraft." + project.getName().toLowerCase()
    archivesBaseName = project.projectDir.name

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    minecraft {
        mappings channel: 'snapshot', version: '20171003-1.12'

        runs {
            client {
                properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
                properties 'forge.logging.console.level': 'debug'
                workingDirectory project.file('run')
                source sourceSets.main
            }
            server {
                properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
                properties 'forge.logging.console.level': 'debug'
                workingDirectory project.file('run')
                source sourceSets.main
            }
        }
    }

    processResources {
        // This will ensure that this task is redone when the versions change.
        inputs.property "version", project.version
        inputs.property "mc_version", '1.12.2'

        // Replace stuff in mcmod.info, nothing else
        from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'

            // Replace version and mcversion
            expand 'version': project.version, 'mc_version': '1.12.2'
        }

        // Copy everything else that's not the mcmod.info
        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
        }
    }

    dependencies {
        minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
        implementation 'com.google.code.findbugs:jsr305:3.0.2'
    }

    jar.finalizedBy('reobfJar')
}
