/*
Build script for all mods.
In principle, building a mod is as simple as adding an empty build.gradle to the mod's directory and updating
settings.gradle.
*/

// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
    }
}

subprojects {
    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'idea'

    String modName = project.getName().replaceAll(/([a-z])([A-Z])/, /$1_$2/).toLowerCase()

    version = "1.17.1-DEV.0"
    group = "com.tomboshoven.minecraft." + project.getName().toLowerCase()
    archivesBaseName = project.projectDir.name

    sourceCompatibility = '16'
    targetCompatibility = '16'

    minecraft {
        mappings channel: 'official', version: '1.17.1'

        runs {
            client {
                properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
                properties 'forge.logging.console.level': 'debug'
                workingDirectory project.file('run')
                source sourceSets.main
            }
            server {
                properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
                properties 'forge.logging.console.level': 'debug'
                workingDirectory project.file('run')
                source sourceSets.main
            }
            data {
                properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
                properties 'forge.logging.console.level': 'debug'
                properties 'fml.earlyprogresswindow': 'false'
                args '--mod', modName, '--all',
                        '--existing', file('src/main/resources/'),
                        '--existing', file('src/generated/resources/'),
                        '--output', file('src/generated/resources/')
                workingDirectory project.file('run')
                source sourceSets.main
            }
        }
    }

    // Add generated resources
    sourceSets.main.resources {
        srcDir 'src/generated/resources'
        include 'assets/**', 'data/**', 'META-INF/**', 'pack.mcmeta'
    }

    dependencies {
        minecraft "net.minecraftforge:forge:1.17.1-37.0.9"

        implementation 'com.google.code.findbugs:jsr305:3.0.2'
    }

    jar {
        manifest {
            attributes([
                "Implementation-Version": getArchiveVersion(),
            ])
        }
    }
}
